#!/bin/zsh
# Per-interactive-shell settings.

# autostart the graphical environment
[ "$(tty)" = /dev/tty1 ] && exec startx

# some useful aliases
alias diff='diff --color=auto'
alias grep='grep --color=auto'
alias ls='ls --color=auto'
alias la='ls -A'
alias ll='ls -lhp'
alias lla='ll -A'
alias ..='cd ..'
alias ...='cd ../../'
alias mv='mv -iv'
alias rm='rm -Ivd'
alias cp='cp -iv'
alias ln='ln -iv'
alias e='$EDITOR'
alias p='$PAGER'
alias gdb='gdb -q'
alias R='R -q --no-save'
alias fzf='fzf --layout=reverse'
alias s='systemctl --user'
alias j='journalctl --user -e'

# some useful options
setopt nohashcmds          # no path hashing
setopt rmstarsilent        # no rm glob warnings
setopt nohup               # don't sighup jobs on exit
setopt nocheckrunningjobs  # report only suspended jobs on exit
setopt interactivecomments # enablie repl comments
setopt prompt_subst        # prompt reevaluation
setopt histignoredups      # ignore "local" history duplicates

# "local" history
HISTSIZE=1000

# disable ctrl-s
stty -ixon

# autocompletion
autoload -U compinit && compinit -d ~/.zsh/compdump
zstyle ':completion:*' menu select

# line editing
bindkey '^u' backward-kill-line
bindkey '^[n' history-beginning-search-forward
bindkey '^[p' history-beginning-search-backward
bindkey '^[f'  emacs-forward-word
bindkey '^[[Z' reverse-menu-complete
WORDCHARS='~-_*.'

# we need this to load hooks
autoload -Uz add-zsh-hook

# prompt
_generate_prompt() {
	err='%(0?//%F{red}%?%f:)'
	job='%(1j/%F{magenta}%jj%f:/)'
	git=$(git status --short 2>/dev/null) && {
		case $git in
			'') git="%F{green}g%f:" ;;
			*)  git="%F{red}g%f:"
		esac
	}
	echo "%n@%M:%~ ${git}${job}${err}%# "
}; PROMPT='$(_generate_prompt)'

# deduplicate and trim lines (useful for history-like stuff)
deduptrim() {
	[ -f "$1" ] && tmp=$(mktemp ~/.cache/XXXXX) && {
		tac "$1" | awk '!seen[$0]++' | head -n "$2" | tac >"$tmp"
		command mv "$tmp" "$1"
	}
}

# directory history
cd() {
	builtin cd "$@" || return
	[[ $PWD = (~|/proc/*|/tmp/*|~/dl/*) ]] && return
	echo "$PWD" >>~/.zsh/dirhist
}

_dirhist() {
	deduptrim ~/.zsh/dirhist 100 || return
	dir=$(fzf --tac +s --prompt='cd> ' <~/.zsh/dirhist)
	[ "$dir" ] || return
	cd "$dir"
	zle reset-prompt
}; zle -N _dirhist && bindkey '^t' _dirhist

# "global" history
_savehist() {
	trimmed=$(echo -E "$1")
	[[ $trimmed = (' '*|'#'*|?|??|???|????|'cd '*|'man '*) ]] && return
	serialized=$(
		echo -nE "$trimmed" | sed -Ez 's/\\/\\\\/g;s/\n/\\n/g'
	)
	echo -E "$serialized" >>~/.zsh/cmdhist
}; add-zsh-hook preexec _savehist

_hist() {
	deduptrim ~/.zsh/cmdhist 1000 || return
	item=$(fzf -q "$LBUFFER" --tac +s --prompt='run> ' <~/.zsh/cmdhist)
	[ "$item" ] || return
	deserialized=$(
		echo -E "$item" | sed -Ez 's/((^|[^\])(\\\\)*)\\n/\1\n/g;s/\\\\/\\/g'
	)
	LBUFFER=$deserialized RBUFFER=
}; zle -N _hist && bindkey '^r' _hist

# terminal title
_ptitle() {
	printf "\e]0;%s\a" "zsh: ${PWD/$HOME/~}"
}; add-zsh-hook precmd _ptitle

_etitle() {
	printf "\e]0;%s\a" "zsh! $1"
}; add-zsh-hook preexec _etitle
